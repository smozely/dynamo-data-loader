buildscript {
    repositories {
        maven {
            url 'https://dl.bintray.com/smozely/maven'
        }
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.4.+'
    }
}

repositories {
    maven {
        url 'https://dl.bintray.com/smozely/maven'
    }
    jcenter()
    mavenCentral()
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'maven-publish'
apply plugin: 'groovy'

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    gitVersionName = "${git.describe()}"
}

group = 'com.smozely'
version = ext.gitVersionName


dependencies {
    compile('com.amazonaws:aws-java-sdk:1.10.26')
    compile('com.google.guava:guava:18.0')

    testCompile('junit:junit:4.12')

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile('org.codehaus.groovy:groovy-all:2.4.5')
    testCompile('org.assertj:assertj-core:3.1.0')
    testCompile('com.michelboudreau:alternator:0.12.0')
}

def isCiBuild() {
    return project.hasProperty("ci-build")
}

if (isCiBuild()) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    bintray {
        user = BINTRAY_USER_NAME
        key  = BINTRAY_API_KEY
        publications = ['mavenJava']
        publish = true
        pkg {
            name = 'dynamo-data-loader'
            repo = 'maven'
        }
    }
}