buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.4.+'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'


ext {
    git = org.ajoberstar.grgit.Grgit.open(file('.'))
    gitVersionName = "${git.describe()}"
}

group = 'com.smozely'
version = ext.gitVersionName

repositories {
    maven {
        url "s3://repo.smozely.com/releases"
        credentials(AwsCredentials) {
            accessKey AWS_ACCESS_KEY
            secretKey AWS_SECRET_KEY
        }
    }

    maven {
        url "s3://repo.smozely.com/snapshots"
        credentials(AwsCredentials) {
            accessKey AWS_ACCESS_KEY
            secretKey AWS_SECRET_KEY
        }
    }

    mavenCentral()
}

dependencies {
    compile 'com.smozely:test:1.0-SNAPSHOT'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            if(project.version.endsWith('-SNAPSHOT')) {
                url "s3://repo.smozely.com/snapshots"
            } else {
                url "s3://repo.smozely.com/release"
            }
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
    }
}