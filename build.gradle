buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:1.4.+'
    }
}

apply plugin: 'groovy'

ext {
    git = org.ajoberstar.grgit.Grgit.open()
    gitVersionName = "${git.describe()}"
}

group = 'com.smozely'
version = ext.gitVersionName

repositories {
    maven {
        url "s3://repo.smozely.com/release"
        credentials(AwsCredentials) {
            accessKey AWS_ACCESS_KEY
            secretKey AWS_SECRET_KEY
        }
    }

    mavenCentral()
}

dependencies {
    compile('com.amazonaws:aws-java-sdk:1.10.26')
    compile('com.google.guava:guava:18.0')

    testCompile('junit:junit:4.12')

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile('org.codehaus.groovy:groovy-all:2.4.5')
    testCompile('org.assertj:assertj-core:3.1.0')
    testCompile('com.michelboudreau:alternator:0.12.0')
}

def isCiBuild() {
    return project.hasProperty("ci-build")
}


if (isCiBuild()) {
    apply plugin: 'maven-publish'
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url "s3://repo.smozely.com/release"
                credentials(AwsCredentials) {
                    accessKey AWS_ACCESS_KEY
                    secretKey AWS_SECRET_KEY
                }
            }
        }
    }
}